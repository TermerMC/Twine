buildscript {
    ext.kotlin_version = '1.3.50'
}

plugins {
	id 'com.github.johnrengelman.shadow' version '5.1.0'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

archivesBaseName = 'twine'
group = 'net.termer.twine'
version = '1.5'

jar {
   manifest {
       attributes 'Main-Class': 'net.termer.twine.Twine'
   }
}

repositories {
    jcenter()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.google.guava:guava:23.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'twine'
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'twine'
				description = 'A fully featured application server built for boilerplate-less Vert.x web applications'
				url = 'https://github.com/termermc/twine'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'termer'
						name = 'Michael Termer'
						email = 'termer@protonmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/termermc/twine.git'
					developerConnection = 'scm:git:ssh://github.com/termermc/twine.git'
					url = 'https://github.com/termermc/twine'
				}
			}
		}
	}
	repositories {
		maven {
			name = 'OSSRH'
			url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			credentials {
				username = nexusUsername
				password = nexusPassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}


javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    preferProjectModules()
	
    force 'org.json:json:20190722'
  }
}

dependencies {
	// Vert.x dependencies
	implementation 'io.vertx:vertx-core:3.9.1'
	implementation 'io.vertx:vertx-web:3.9.1'
	implementation 'io.vertx:vertx-sql-common:3.9.1'
	implementation 'io.vertx:vertx-jdbc-client:3.9.1'
	implementation 'io.vertx:vertx-zookeeper:3.9.1'
	implementation "io.vertx:vertx-lang-kotlin:3.9.1"
	implementation "io.vertx:vertx-lang-kotlin-coroutines:3.9.1"
	
	// Kotlin dependencies
	implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.72'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6'
	
	// Misc. dependencies
	implementation group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
	implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.5.2'
	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
	implementation group: 'org.beanshell', name: 'bsh', version: '2.0b5'
	implementation group: 'org.json', name: 'json', version: '20190722'
	
	// SLF4J dependencies
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
	
	// Database dependencies
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.12'
	implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.20'
	implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5'
}