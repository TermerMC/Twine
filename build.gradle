buildscript {
    ext.kotlin_version = '1.5.21'
}

plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'twine'
group = 'net.termer.twine'
version = '2.2'

jar {
   manifest {
       attributes 'Main-Class': 'net.termer.twine.Twine'
   }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.google.guava:guava:30.1.1-jre'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

java {
	withJavadocJar()
	withSourcesJar()
}

components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
	skip()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'twine'
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'twine'
				description = 'A fully featured application server built for boilerplate-less Vert.x web applications'
				url = 'https://github.com/termermc/twine'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'termer'
						name = 'Michael Termer'
						email = 'termer@protonmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/termermc/twine.git'
					developerConnection = 'scm:git:ssh://github.com/termermc/twine.git'
					url = 'https://github.com/termermc/twine'
				}
			}
		}
	}
	repositories {
		maven {
			name = 'OSSRH'
			url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			credentials {
				username = nexusUsername
				password = nexusPassword
			}
		}
	}
}

project.configurations.archives.artifacts.clear();

signing {
	sign publishing.publications.mavenJava
}


javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    preferProjectModules()
	
    force 'org.json:json:20190722'
  }
}

dependencies {
	// Vert.x dependencies
	api 'io.vertx:vertx-core:4.1.2'
	api 'io.vertx:vertx-web:4.1.2'
	api 'io.vertx:vertx-zookeeper:4.1.2'
	api "io.vertx:vertx-lang-kotlin:4.1.2"
	api "io.vertx:vertx-lang-kotlin-coroutines:4.1.2"
	api 'io.vertx:vertx-web-validation:4.1.2'
	
	// Kotlin dependencies
	api 'org.jetbrains.kotlin:kotlin-stdlib:1.4.21'
	api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
	
	// Misc. dependencies
	implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.9.0'
	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'
	implementation group: 'org.json', name: 'json', version: '20190722'
	
	// SLF4J dependencies
	api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
	api group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.32'
	
	// Database dependencies
	api 'io.vertx:vertx-pg-client:4.1.2'
	api 'io.vertx:vertx-mysql-client:4.1.2'
}

// Execute shadowJar when running build
tasks.build.dependsOn tasks.shadowJar